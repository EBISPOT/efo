PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dbpedia2: <http://dbpedia.org/property/>
PREFIX dbpedia: <http://dbpedia.org/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?term ?term_label ?thing ?thing_label ?p ?genus

WHERE {
  {
  ?term rdf:type owl:Class ;
  rdfs:subClassOf+ <http://www.ebi.ac.uk/efo/EFO_0001444> ;
  rdfs:subClassOf [ rdf:type owl:Restriction ;
                         owl:onProperty <http://purl.obolibrary.org/obo/IAO_0000136> ;
                         owl:someValuesFrom ?thing
                       ] .
   OPTIONAL {
     ?term rdfs:label ?term_label .
   }
   OPTIONAL {
    ?thing rdfs:label ?thing_label .
   }
   BIND("subclass" as ?p)
   FILTER(isIRI(?term))
   FILTER(isIRI(?thing))
 } UNION {
   ?term rdf:type owl:Class ;
   rdfs:subClassOf+ <http://www.ebi.ac.uk/efo/EFO_0001444> ;
   owl:equivalentClass [ rdf:type owl:Restriction ;
                          owl:onProperty <http://purl.obolibrary.org/obo/IAO_0000136> ;
                          owl:someValuesFrom ?thing
                        ] .
    OPTIONAL {
      ?term rdfs:label ?term_label .
    }
    OPTIONAL {
     ?thing rdfs:label ?thing_label .
    }
    BIND("equiv" as ?p)
    FILTER(isIRI(?term))
    FILTER(isIRI(?thing))
 } UNION {
   ?term rdf:type owl:Class ;
   rdfs:subClassOf+ <http://www.ebi.ac.uk/efo/EFO_0001444> ;
   owl:equivalentClass [ owl:intersectionOf ( ?genus
                                              [ rdf:type owl:Restriction ;
                                                owl:onProperty <http://purl.obolibrary.org/obo/IAO_0000136> ;
                                                owl:someValuesFrom ?thing
                                              ]
                                            ) ;
                         rdf:type owl:Class
                       ] .
    OPTIONAL {
      ?term rdfs:label ?term_label .
    }
    OPTIONAL {
     ?thing rdfs:label ?thing_label .
    }
    BIND("genus_equiv" as ?p)
    FILTER(isIRI(?term))
    FILTER(isIRI(?thing))
 }
}

### owl:equivalentClass [ rdf:type owl:Restriction ;
#                       owl:onProperty <http://purl.obolibrary.org/obo/IAO_0000136> ;
#                       owl:someValuesFrom ?thing
  #                   ] .

# <http://www.ebi.ac.uk/efo/EFO_0004298> rdf:type owl:Class ;
                                       # owl:equivalentClass [ owl:intersectionOf ( <http://www.ebi.ac.uk/efo/EFO_0001444>
                                       #                                            [ rdf:type owl:Class ;
                                       #                                              owl:unionOf ( [ rdf:type owl:Restriction ;
                                       #                                                              owl:onProperty <http://purl.obolibrary.org/obo/IAO_0000136> ;
                                       #                                                              owl:someValuesFrom <http://purl.obolibrary.org/obo/UBERON_0004535>
                                       #                                                            ]
                                       #                                                            [ rdf:type owl:Restriction ;
                                       #                                                              owl:onProperty <http://purl.obolibrary.org/obo/IAO_0000136> ;
                                       #                                                              owl:someValuesFrom <http://www.ebi.ac.uk/efo/EFO_0000319>
                                       #                                                            ]
                                       #                                                          )
                                       #                                            ]
                                       #                                          ) ;
                                       #                       rdf:type owl:Class
                                       #                     ] .
